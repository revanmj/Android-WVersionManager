apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

ext {
    bintrayRepo = "Android-WVersionManager"
    bintrayName = "com.winsontan520.wversionmanager"
    libraryDescription = ''

    publishedGroupId = 'com.winsontan520.wversionmanager'
    libraryName = 'wversionmanager'
    artifact = 'wversionmanager'

    siteUrl = 'https://github.com/revanmj/Android-WVersionManager'
    gitUrl = 'https://github.com/revanmj/Android-WVersionManager.git'

    libraryVersion = "1.7.2"

    developerId = 'revanmj'
    developerName = 'Michal Jakubowski'
    developerEmail = 'revanmj@@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 10
        versionName "$libraryVersion"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
}

task deleteJar(type: Delete) {
    delete 'libs/wversionmanager.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('libs/')
    include('classes.jar')
    rename('classes.jar', 'wversionmanager.jar')
}

createJar.dependsOn(deleteJar, build)

//Add these lines to publish library to bintray. This is the readymade scripts made by github user nuuneoi to make uploading to bintray easy.
//Place it at the end of the file
if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
}

def githubProperties = new Properties()
try {
    githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
} catch (all) {
    println('github.properties file does not exist!')
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId "$publishedGroupId" // Replace with group ID
            artifactId "$libraryName"
            version "$libraryVersion"
            artifact("$buildDir/outputs/aar/$libraryName-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            /** Configure path of your package repository on Github
             ** Replace GITHUB_USERID with your/organisation Github userID
             ** and REPOSITORY with the repository name on GitHub
             */
            url = uri("https://maven.pkg.github.com/revanmj/Android-WVersionManager")
            credentials {
                /** Create github.properties in root project folder file with
                 ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN
                 ** Set env variable GPR_USER & GPR_API_KEY if not adding a properties file**/
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}
